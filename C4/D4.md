# [click here to see this on playground](https://play.onflow.org/f0581871-5756-45de-9052-7f9f65fb9095?type=account&id=11b283f2-c276-46cd-96ba-340a98d01e17&storage=none)
# Contract
```cadence
pub contract CryptoPoops {
  pub var totalSupply: UInt64

  // This is an NFT resource that contains a name,
  // favouriteFood, and luckyNumber
  pub resource NFT {
    pub let id: UInt64

    pub let name: String
    pub let favouriteFood: String
    pub let luckyNumber: Int

    init(_name: String, _favouriteFood: String, _luckyNumber: Int) {
      self.id = self.uuid

      self.name = _name
      self.favouriteFood = _favouriteFood
      self.luckyNumber = _luckyNumber
    }
  }

  // This is a resource interface that allows us to get access only specific functions.
  pub resource interface CollectionPublic {
    pub fun deposit(token: @NFT)
    pub fun getIDs(): [UInt64]
    pub fun borrowNFT(id: UInt64): &NFT
  }

  // This is a resource that allows us to store all nft resource in one place.
  pub resource Collection: CollectionPublic {
    pub var ownedNFTs: @{UInt64: NFT}

    // this function allow anybody to deposit a nft
    pub fun deposit(token: @NFT) {
      self.ownedNFTs[token.id] <-! token
    }

    // this function allow only AuthAccount to withdraw a nft
    pub fun withdraw(withdrawID: UInt64): @NFT {
      let nft <- self.ownedNFTs.remove(key: withdrawID) 
              ?? panic("This NFT does not exist in this Collection.")
      return <- nft
    }

    // this function allow anybody to see all the ids of nfts in this collection
    pub fun getIDs(): [UInt64] {
      return self.ownedNFTs.keys
    }

    // this function allow anybody to read the metadata od a nft
    pub fun borrowNFT(id: UInt64): &NFT {
      return (&self.ownedNFTs[id] as &NFT?)!
    }

    init() {
      self.ownedNFTs <- {}
    }

    destroy() {
      destroy self.ownedNFTs
    }
  }

  // this function allow anybody to create empty collection resource
  pub fun createEmptyCollection(): @Collection {
    return <- create Collection()
  }

  // this resource stores the ability to create nfts and minter resource
  pub resource Minter {

    // this function allow only AuthAccount to create nft resource
    pub fun createNFT(name: String, favouriteFood: String, luckyNumber: Int): @NFT {
      return <- create NFT(_name: name, _favouriteFood: favouriteFood, _luckyNumber: luckyNumber)
    }

   // this function allow only AuthAccount to create minter resource
    pub fun createMinter(): @Minter {
      return <- create Minter()
    }

  }

  init() {
    self.totalSupply = 0
    self.account.save(<- create Minter(), to: /storage/Minter)
  }
}
```
#### the other ways to distribute `Minters` to other people WITHOUT having to have 2 signers can be done in two steps:
* 1st the `creator` should `borrow` the `minter` reference from storage and create a `private` `link` of `minter` reference and give it to `recipient`.
* then the `recipient` should create `Capability` to borrow the reference of `minter` and create a new `minter` and `save` to his storage.

