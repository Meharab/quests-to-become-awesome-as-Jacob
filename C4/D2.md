#### 1. `.link()` is a function that allows uses to expose theirs datas to the `/public/` or `/private/` path from `/storage/` path, so that other uses can use that link to read the data.

#### 2. we can use resource interfaces to only expose certain things to the `/public/` path because a resource may contain a lot of functions and variables but resource interface not necessarily needs to contain all the function and variable that a resource have. So we can design resource interface in such a way that we want to expose to `/public/` path. So that we can use that resource interface to put restrictions on `.capability()` and `.link()` to control the outcome what we are exposing to `/public/` path.

# 3. [Click Here to Access Playground ](https://play.onflow.org/c352275b-d8a0-45d9-9882-ae2961a83411)
# Contract
```cadence
pub contract Anime {

  pub resource interface ReadOnly {
    pub var name: String
  }

  pub resource List: ReadOnly {
    pub var name: String

    pub fun addList(newName: String) {
      self.name = newName
    }

    init() {
      self.name = "A Silent ðŸ˜¶ Voice!"
    }
  }

  pub fun createList(): @List {
    return <- create List()
  }

}
```
# i) Transaction
```cadence
import Anime from 0x01
transaction() {
  prepare(acc: AuthAccount) {
    acc.save(<- Anime.createList(), to: /storage/MyResource)
    acc.link<&Anime.List{Anime.ReadOnly}>(/public/MyResource, target: /storage/MyResource)
  }

  execute {
    log("successfully done")
  }
}
```
# ii) Error Script
```cadence
import Anime from 0x01
pub fun main(add: Address) {
  let publicCapability: Capability<&Anime.List{Anime.ReadOnly}> =
    getAccount(add).getCapability<&Anime.List{Anime.ReadOnly}>(/public/MyResource)

  let readResource: &Anime.List{Anime.ReadOnly} = publicCapability.borrow() 
    ?? panic("do not panic.")

  // ERROR: "not accessible: addList"
  log(readResource.addList(newName: "prison school"))
}
```
# iii) Successful Script
```cadence
import Anime from 0x01
pub fun main(add: Address) {
  let publicCapability: Capability<&Anime.List{Anime.ReadOnly}> =
    getAccount(add).getCapability<&Anime.List{Anime.ReadOnly}>(/public/MyResource)

  let readResource: &Anime.List{Anime.ReadOnly} = publicCapability.borrow() 
    ?? panic("do not panic.")

  log(readResource.name)
}
```
